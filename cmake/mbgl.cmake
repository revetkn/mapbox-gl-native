if (NOT DEFINED MBGL_PLATFORM)
    if (CMAKE_HOST_SYSTEM_NAME STREQUAL "Darwin")
        set(MBGL_PLATFORM "osx")
    else()
        set(MBGL_PLATFORM "linux")
    endif()
endif()

function(target_add_shader target name type)
    set(shader_file ${CMAKE_SOURCE_DIR}/node_modules/mapbox-gl-shaders/src/${name}.${type}.glsl)
    set(shader_source ${CMAKE_CURRENT_BINARY_DIR}/include/mbgl/shader/${name}.${type}.hpp)
    add_custom_command(
       OUTPUT ${shader_source}
       COMMAND ${CMAKE_SOURCE_DIR}/scripts/build-shaders.py ${shader_file} ${shader_source}
       DEPENDS ${shader_file} npm-update
    )
    target_sources(${target} PRIVATE ${shader_source})
    source_group(include\\mbgl\\shader FILES ${shader_source})
endfunction()

function(target_add_shaders target)
    foreach(name ${ARGN})
        target_add_shader(${target} ${name} vertex)
        target_add_shader(${target} ${name} fragment)
    endforeach()
endfunction()

# Generate source groups so the files are properly sorted in IDEs like Xcode.
function(create_source_groups target)
    get_target_property(sources ${target} SOURCES)
    foreach(file ${sources})
        get_filename_component(file "${file}" ABSOLUTE)
        string(REGEX REPLACE "^${CMAKE_SOURCE_DIR}/" "" group "${file}")
        get_filename_component(group "${group}" DIRECTORY)
        string(REPLACE "/" "\\" group "${group}")
        source_group("${group}" FILES "${file}")
    endforeach()
endfunction()

add_custom_command(
    OUTPUT ${CMAKE_BINARY_DIR}/include/mbgl/util/version.hpp
    DEPENDS .git/HEAD
    COMMAND ${CMAKE_SOURCE_DIR}/scripts/build-version.py ${CMAKE_BINARY_DIR}
    VERBATIM
)
add_custom_target(mbgl-version-header
    DEPENDS ${CMAKE_BINARY_DIR}/include/mbgl/util/version.hpp
)

function(_write_xcconfig_var target var)
    get_property(result TARGET ${target} PROPERTY INTERFACE_${var} SET)
    if (result)
        get_property(result TARGET ${target} PROPERTY INTERFACE_${var})
        string(REPLACE ";" "\" \"" result "${result}")
        string(REPLACE "-" "_" target "${target}")
        file(APPEND "${CMAKE_BINARY_DIR}/config.xcconfig" "${target}_${var} = \"${result}\"\n")
    endif()
endfunction()

function(target_append_xcconfig target)
    file(APPEND "${CMAKE_BINARY_DIR}/config.xcconfig" "\n// ${target}\n")
    _write_xcconfig_var(${target} INCLUDE_DIRECTORIES)
    _write_xcconfig_var(${target} COMPILE_DEFINITIONS)
    _write_xcconfig_var(${target} COMPILE_OPTIONS)
    _write_xcconfig_var(${target} LINK_LIBRARIES)
endfunction()

file(WRITE "${CMAKE_BINARY_DIR}/config.xcconfig" "// Do not edit -- generated by CMake\n")

set(CMAKE_OSX_DEPLOYMENT_TARGET 10.10)
